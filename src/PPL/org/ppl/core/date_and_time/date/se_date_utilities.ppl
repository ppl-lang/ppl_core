///
   Copyright (C) 2009-2014 Christian Neumanns (www.practical-programming.org)
   This code can be used under the terms of the 'GNU Afero General Public License version 3'
   The full text of this license can be found at http://www.gnu.org/licenses/agpl.html
   THIS CODE IS DISTRIBUTED WITHOUT ANY WARRANTY. See the license for details.
end ///

service date_utilities

	function days_in_month ( year, month ) -> positive_32
	
	    case value of i_month.value
	        when 4, 6, 9, 11
	            return 30
            when 2
                return if i_year.is_leap_year then 29 else 28
            otherwise
                return 31
        .
    .

	function is_leap_year ( year positive_32 ) -> yes_no
		script
			return i_year.remainder ( 4 ) =v 0 and &
				( i_year.remainder ( 100 ) #v 0 or i_year.remainder ( 400 ) =v 0 )
		.
		tests
            verify is_leap_year ( 2004 )
            verify not is_leap_year ( 2005 )
            verify not is_leap_year ( 2006 )
            verify not is_leap_year ( 2007 )
            verify is_leap_year ( 2008 )
            verify not is_leap_year ( 2009 )
            verify not is_leap_year ( 2010 )
            verify not is_leap_year ( 2011 )
            verify is_leap_year ( 2012 )

            // 100
            verify not is_leap_year ( 1900 )
            verify is_leap_year ( 2000 )
            verify not is_leap_year ( 2100 )
            verify not is_leap_year ( 2200 )
            verify not is_leap_year ( 2300 )
            verify is_leap_year ( 2400 )
        .
    .
.
