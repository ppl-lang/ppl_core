///
   Copyright (C) 2009-2014 Christian Neumanns (www.practical-programming.org)
   This code can be used under the terms of the 'GNU Afero General Public License version 3'
   The full text of this license can be found at http://www.gnu.org/licenses/agpl.html
   THIS CODE IS DISTRIBUTED WITHOUT ANY WARRANTY. See the license for details.
end ///

factory test_context type:test_context

	attributes kind:variable access:private
	
		private_RSEs_tested_count zero_positive_32 default:0

		private_test_error_list mutable_indexed_list<test_error> default:mutable_indexed_list<test_error>.create
    .
	
	function RSEs_tested_count = a_private_RSEs_tested_count
	
	function test_errors_found = not a_private_test_error_list.is_empty
	
	function test_error_count = a_private_test_error_list.size
	
	function test_error_list = a_private_test_error_list.copy_to_immutable
	
	command start
		script
			a_private_RSEs_tested_count = 0
			a_private_test_error_list = mutable_indexed_list<test_error>.create
		end
	end

	command stop
		script
			a_message_handler.testing_finished ( &
				RSEs_tested_count = a_private_RSEs_tested_count &
				test_error_count = co_test_error_count )
		end
	end


	command handle_RSE_test_start
		script
			a_private_RSEs_tested_count = a_private_RSEs_tested_count + 1

			co_handle_info_message ( """testing {{i_RSE_id_path}}""" )
		end
	end

	command handle_test_error
		script
			a_private_test_error_list.append ( i_test_error )

			a_message_handler.handle_test_error ( i_test_error )
		end
	end

	command handle_info_message
		script
			if a_display_info_messages then
				a_message_handler.handle_info_message ( i_message )
			end if
		end
	end

	creator create kind:in_all end
	
end